\ 清除 `-nc` 之後所定義的 Forth 命令
-nc

\ 用來儲存 Jogging target position 參數
create jogging-positions falign 0e f, 0e f, 0e f, 0e f,
: jogging-position! ( index -- )( F: pos -- )
    floats jogging-positions faligned + f!
    ;
: jogging-position@ ( index -- )( F: -- pos )
    floats jogging-positions faligned + f@
    ;

\ 開始進行回歸機械原點
\ 可以規劃不同的判斷邏輯，發出回歸原點的指令或是啟動回歸機械原點的 SFC。
\ 要搭配 +homing-axis ( index priority -- ) 命令使用
: start-homing ( -- )
    true homing-accepted !
    motion-idle? not if false homing-accepted ! ." log|Not Motion Idle" cr then
    servo-on? not if false homing-accepted ! ." log|Not Servo On" cr then
    event-acked? not if false homing-accepted ! ." log|No Error Event Acknowledge " cr then
    homing-accepted @ if
        1 motion-state !
        start-axes-homing
    then
    ;

\ 開始進行軸移動
\ 可以規劃不同的判斷邏輯，發出軸運動指令或是啟動軸運動的 SFC。
: start-jogging ( -- )( F: x y z -- )
    3 jogging-position! 2 jogging-position! 1 jogging-position!
    enter-stack-check
    true jogging-accepted !
    motion-idle? not if false jogging-accepted ! ." log|Not Motion Idle" cr then
    servo-on? not if false jogging-accepted ! ." log|Not Servo On" cr then
    event-acked? not if false jogging-accepted ! ." log|No Error Event Acknowledge " cr then
    jogging-accepted @ if
        2 motion-state !
        1
        begin
            dup axes-len <=
        while
            dup axis@
            dup +interpolator
            rapid-travels-rate@ dup interpolator-v!
            over jogging-position@ axis-cmd-p!
            1+
        repeat
        drop
    then
    leave-stack-check
    ;

\ 定義 machining 停止狀態
variable machining-stopping
\ 定義 machining 加工完成
variable machining-finished

\ 開始進行加工
\ 可以規劃不同的判斷邏輯，發出加工指令或是啟動軸運動的 SFC。
: start-machining  ( -- )
    true machining-accepted !
    motion-idle? not if false machining-accepted ! ." log|Not Motion Idle" cr then
    servo-on? not if false machining-accepted ! ." log|Not Servo On" cr then
    event-acked? not if false machining-accepted ! ." log|No Error Event Acknowledge " cr then
    machining-accepted @ if
        3 motion-state !
        resume-nc 1 group! +group start-job
        false machining-stopping !
        false machining-finished !
    then
    ;

\ Reset machinig
 : reset-machining  ( -- )
    motion-idle? if
        reset-job kill-nc
        false machining-stopping !
        false machining-finished !
    else
        ." log|Not Motion Idle" cr
    then
    ;

\ 要求運動停止
\ 可以依運動模式發出不同的停止命令
: stop-motion  ( -- )
    motion-idle? not if
        motion-state @ case
            1 of    
                stop-axes-homing
            endof
            2 of 
                1
                begin
                    dup axes-len <=
                while
                    dup axis@ -interpolator
                    1+
                repeat
                drop
            endof
            3 of
                stop-job suspend-nc
                true machining-stopping !
            endof
        endcase
    else
        ." log|Motion Idle" cr
    then
    ;

\ 緊急停止
: ems-stop
    ems-motion
    ems-servo-off
    ems-job
    kill-nc
    false machining-stopping !
    false machining-finished !
    ;

\ Homing state 停止的條件
: homing-stopping? ( -- flag )
    homing-priority @ 0=
    ;

\ Jogging state 停止的條件
variable is-jogging-stopping
: jogging-stopping? ( -- flag )
    true is-jogging-stopping !
    1
    begin
        dup axes-len <=
        is-jogging-stopping @ and
    while
        dup axis@ interpolator-reached? not if
            false is-jogging-stopping !
        then
        1+
    repeat
    drop
    is-jogging-stopping @
;

\ Machining state 停止的條件
: machining-stopping? ( -- flag )
    machining-stopping @  machining-finished @ or job-stop? and
    ;

\ Machining state 每個周期都要執行的工作
: machining-loop ( -- )
    1 group!
    next-path-mode@ case
        0 of
            rapid-travels-rate@ vcmd!
        endof
        1 of
            machining-rate@ vcmd!
        endof
        10e mm/min vcmd!
    endcase
    ;

\ 重新定義 'homing-stopping, 'jogging-stopping, 'machining-stopping, 'machining-loop
' homing-stopping? 'homing-stopping !
' jogging-stopping? 'jogging-stopping !
' machining-stopping? 'machining-stopping !
' machining-loop 'machining-loop !

\ 定義監控機制所需要的參數
variable monitor-acked
variable ec-ready-once

\ 確認異警
: ack-monitor
	event-acked? if
		0event-level
	else
		true monitor-acked !
	then
	;

\ 輸出監控機制目前所收到的異警等級
: .monitor
    ." monitor_failed|" event-level@ 0 .r cr
    ;

\ 監控機制初始化
\ 等待第一次 EtherCAT 通訊成功    
: monitor-init
    ec-ready-once @ not ec-ready? and  if
        true ec-ready-once !
    then

    ec-ready-once @ if
        0event-level
        false monitor-acked !
        reset-overrun
	else
        report-warning
    then
    ;

\ 每個週期間監控的項目    
: monitor-loop
    \ EtherCAT 通訊
    ec-ready? not if
        ." error|EtherCAT communication error !" cr
        report-error
    then

    \ real time cycle overrun
    overrun? if
        ." error|System Overrun !" cr
        report-error
    then

    \ 依據 event-level@ 進行不同的處置
    event-level@ error-class >= if
        ems-stop
    else
        event-level@ warning-class >= if
            stop-motion
        then
    then
    ;

\ Monitor Post action    
: monitor-post
    ;

\ 從 Monitor Init 切換到 Motion Loop 的條件
: monitor2loop?
    event-level@ 0 =
    ;

\ 從 Monitor Loop 切換到 Motion Post Action 的條件
: monitor2post?
    event-level@ warning-class >=
;

\ 從 Monitor Post Action 切換到 Motion Init 的條件
: monitor2init?
    monitor-acked @
;

\ 宣告 Monitor SFC Step
step monitor-init
step monitor-loop
step monitor-post

\ 宣告 Monitor SFC transition
transition  monitor2loop?
transition  monitor2post?
transition  monitor2init?

\ 連結 Monitor SFC
' monitor-init     ' monitor2loop? -->
' monitor2loop?    ' monitor-loop  -->
' monitor-loop     ' monitor2post? -->
' monitor2post?    ' monitor-post  -->
' monitor-post     ' monitor2init? -->
' monitor2init?    ' monitor-init  -->


\ 啟動 SFC step
' motion-idle +step
' devices-check +step
' monitor-init +step

\ 標記 -nc
marker -nc

-nc

\ 用來儲存 Jogging target position 參數
create jogging-positions falign 0e f, 0e f, 0e f, 0e f,
: jogging-position! ( index -- )( F: pos -- )
    floats jogging-positions faligned + f!
    ;
: jogging-position@ ( index -- )( F: -- pos )
    floats jogging-positions faligned + f@
    ;

\ 開始進行回歸機械原點
\ 可以規劃不同的判斷邏輯，發出回歸原點的指令或是啟動回歸機械原點的 SFC。
\ 要搭配 +homing-axis ( index priority -- ) 命令使用
: start-homing ( -- )
    true homing-accepted !
    motion-idle? not if false homing-accepted ! ." log|Not Motion Idle" cr then
    servo-on? not if false homing-accepted ! ." log|Not Servo On" cr then
    event-acked? not if false servo-on-accepted ! ." log|No Error Event Acknowledge " cr then
    homing-accepted @ if
        1 motion-state !
        start-axes-homing
    then
    ;

\ 開始進行軸移動
\ 可以規劃不同的判斷邏輯，發出軸運動指令或是啟動軸運動的 SFC。
: start-jogging ( -- )( F: x y z -- )
    true jogging-accepted !
    motion-idle? not if false jogging-accepted ! ." log|Not Motion Idle" cr then
    servo-on? not if false jogging-accepted ! ." log|Not Servo On" cr then
    event-acked? not if false servo-on-accepted ! ." log|No Error Event Acknowledge " cr then
    3 jogging-position! 2 jogging-position! 1 jogging-position!
    jogging-accepted @ if
        2 motion-state !
        1
        begin
            dup axes-len <=
        while
            dup axis@
            dup +interpolator
            rapid-travels-rate@ dup interpolator-v!
            over jogging-position@ axis-cmd-p!
            1+
        repeat
        drop
    then
    ;

\ 定義 machining 停止狀態
variable machining-stopping
\ 定義 machining 加工完成
variable machining-finished

\ 開始進行加工
\ 可以規劃不同的判斷邏輯，發出加工指令或是啟動軸運動的 SFC。
: start-machining  ( -- )
    true machining-accepted !
    motion-idle? not if false machining-accepted ! ." log|Not Motion Idle" cr then
    servo-on? not if false machining-accepted ! ." log|Not Servo On" cr then
    event-acked? not if false servo-on-accepted ! ." log|No Error Event Acknowledge " cr then
    machining-accepted @ if
        3 motion-state !
        resume-nc 1 group! +group start-job
        false machining-stopping !
        false machining-finished !
    then
    ;

\ Reset machinig
 : reset-machining  ( -- )
    motion-idle? if
        reset-job kill-nc
        false machining-stopping !
        false machining-finished !
    then
    ;

\ 要求運動停止
\ 可以依運動模式發出不同的停止命令
: stop-motion  ( -- )
    motion-idle? not if
        motion-state @ case
            1 of    ." log|Stop Homing" cr
                    stop-axes-homing
            endof
            2 of    ." log|Stop Jogging" cr
                    1
                    begin
                        dup axes-len <=
                    while
                        dup axis@ -interpolator
                        1+
                    repeat
                    drop
            endof
            3 of    ." log|Stop machining" cr
                    stop-job suspend-nc
                    true machining-stopping !
            endof
        endcase
    else
        ." log|motion idle" cr
    then
    ;

\ 緊急停止
: ems-stop
    ems-motion
    ems-servo-off
    ems-job
    kill-nc
    false machining-stopping !
    false machining-finished !
    ;

\ Homing state 停止的條件
: homing-stopping? ( -- flag )
    homing-priority @ 0=
    ;

\ Jogging state 停止的條件
variable is-jogging-stopping
: jogging-stopping? ( -- flag )
    true is-jogging-stopping !
    1
    begin
        dup axes-len <=
        is-jogging-stopping @ and
    while
        dup axis@ interpolator-reached? not if
            false is-jogging-stopping !
        then
        1+
    repeat
    drop
    is-jogging-stopping @
;

\ Machining state 停止的條件
: machining-stopping? ( -- flag )
    machining-stopping @  machining-finished @ or job-stop? and
    ;

\ Machining state 每個周期都要執行的工作
: machining-loop ( -- )
    1 group!
    next-path-mode@ case
        0 of
            rapid-travels-rate@ vcmd!
        endof
        1 of
            machining-rate@ vcmd!
        endof
        10e mm/min vcmd!
    endcase
    ;

\ 重新定義 'homing-stopping, 'jogging-stopping, 'machining-stopping, 'machining-loop
' homing-stopping? 'homing-stopping !
' jogging-stopping? 'jogging-stopping !
' machining-stopping? 'machining-stopping !
' machining-loop 'machining-loop !

variable monitor-acked
variable ec-ready-once
: ack-monitor
    event-acked? if
        true monitor-acked !
    else
        0event-level
    then
    ;

: .monitor
    ." monitor_failed|" event-level@ 0 .r cr
    ;

: monitor-init
    ec-ready-once @ not ec-ready? and  if
        true ec-ready-once !
    then

    ec-ready-once @ if
        0event-level
        false monitor-acked !
        reset-overrun
    else
        report-warning
    then
    ;

: monitor-loop
    ec-ready? not if
        ." error|EtherCAT communication error !" cr
        report-error
    then

    overrun? if
        ." error|System Overrun !" cr
        report-error
    then

    event-level@ error-class >= if
        ems-stop
    else
        event-level@ warning-class >= if
            stop-motion
        then
    then

    ;

: monitor-post
    ;

: monitor2loop?
    event-level@ warning-class <
    ;

: monitor2post?
    event-level@ warning-class >=
;

: monitor2init?
    monitor-acked @
;


step monitor-init
step monitor-loop
step monitor-post

transition  monitor2loop?
transition  monitor2post?
transition  monitor2init?

' monitor-init      ' monitor2loop? -->
' monitor2loop?     ' monitor-loop  -->
' monitor-loop      ' monitor2post? -->
' monitor2post?     ' monitor-post  -->
' monitor-post      ' monitor2init? -->
' monitor2init?     ' monitor-init  -->

\ Activate motion-idle step
' motion-idle +step
' devices-check +step
' monitor-init +step

marker -nc

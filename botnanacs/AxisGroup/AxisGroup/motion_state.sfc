-nc

\ 定義是否接受運動模式要求的變數
variable homing-accepted
variable jogging-accepted
variable machining-accepted

\ 定義運動模式是否結束的變數
variable homing-stopped
variable jogging-stopped
variable machining-stopped

\ 要求運動緊急停止
: ems-motion  ( -- )
    true homing-stopped !
    true jogging-stopped !
    true machining-stopped !
    ." log|EMS motion" cr
    ;


\ Motion Idle Step
: motion-idle ( -- )
    ;


variable 'homing-stopping
\ Motion Homing Step
\ 可以在此規劃 Homing 時的活動，與收到停止命令時的處置
: motion-homing ( -- )
    'homing-stopping @ execute if
        true homing-stopped !
    then
    ;

variable 'jogging-stopping
\ Motion Jogging Step
\ 可以在此規劃 Jogging 時的活動，與收到停止命令時的處置
: motion-jogging ( -- )
    'jogging-stopping @ execute if
        true jogging-stopped !
		1
        begin
			dup axes-len <=
        while
			dup axis@ -interpolator
            1+
        repeat
        drop
    then
    ;

variable 'machining-stopping
variable 'machining-loop
\ Motion Machining Step
\ 可以在此規劃 Machining 時的活動，與收到停止命令時的處置
: motion-machining ( -- )
    'machining-loop @ execute
    'machining-stopping @ execute if
        true machining-stopped !
    then
    ;

\ Motion Post Action
\ 運動停止後的處置
: motion-post-action ( -- )
    false homing-accepted !
    false jogging-accepted !
    false machining-accepted !
    false homing-stopped !
    false jogging-stopped !
    false machining-stopped !
    1 group! -group
    0 motion-state !
    ;

\ Is homing accepted ?
\ 是否接受 Start homing 命令 ？
: homing-accepted?  ( -- flag )
    homing-accepted @
    ;

\ Is homing stopped ?
\ Homing 模式是否已經停止 ?
: homing-stopped? ( -- flag )
    homing-stopped @
    ;


\ Is jogging accepted ?
\ 是否接受 Start jogging 命令 ？
: jogging-accepted? ( -- flag )
    jogging-accepted @
    ;

\ Is jogging stopped ?
\ Jogging 模式是否已經停止 ?
: jogging-stopped? ( -- flag )
    jogging-stopped @
    ;

\ Is machining accepted ?
\ 是否接受 Start Machining 命令 ？
: machining-accepted? ( -- flag )
    machining-accepted @
    ;

\ Is Machining stopped ?
\ Machining 模式是否已經停止 ?
: machining-stopped? ( -- flag )
    machining-stopped @
    ;

\ Motion Post Action 自動切換到 Motion Idle
: motion-auto-passed? ( -- flag )
    true
    ;


\ 宣告 SFC Step
step motion-idle
step motion-homing
step motion-jogging
step motion-machining
step motion-post-action

\ 宣告 SFC Transition
transition  homing-accepted?
transition  homing-stopped?
transition  jogging-accepted?
transition  jogging-stopped?
transition  machining-accepted?
transition  machining-stopped?
transition  motion-auto-passed?


\ SFC Link
' motion-idle           ' homing-accepted?    -->
' homing-accepted?      ' motion-homing       -->
' motion-homing         ' homing-stopped?     -->
' homing-stopped?       ' motion-post-action  -->
' motion-post-action    ' motion-auto-passed? -->
' motion-auto-passed?   ' motion-idle         -->

' motion-idle           ' jogging-accepted?   -->
' jogging-accepted?     ' motion-jogging      -->
' motion-jogging        ' jogging-stopped?    -->
' jogging-stopped?      ' motion-post-action  -->

' motion-idle           ' machining-accepted? -->
' machining-accepted?   ' motion-machining    -->
' motion-machining      ' machining-stopped?  -->
' machining-stopped?    ' motion-post-action  -->


marker -nc
